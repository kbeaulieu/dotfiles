#!/usr/bin/env fish

set -g script_path (status --current-filename)
set -g script_dir (dirname $script_path)/wallpaper-changer
set -g earthviews_json $script_dir/earthview.json
set -g wallpapers_dir $script_dir/wallpapers
set -g osa_script $script_dir/change-wallpaper.scpt

function _gen_random
    set -l file_length (jq length $earthviews_json)
    echo (random 0 $file_length)
end

function _download_wallpaper --argument-names url storage_path
    echo Wallpaper not in cache. Fetching:
    curl --progress-bar --output $storage_path $url
end

function _set_wallpaper -a wallpaper_path
    osascript $osa_script $wallpaper_path
end

function main
    set -l file_index
    set -l wallpaper

    getopts $argv | while read -l key value
        switch $key
            case u url-id
                echo "in -u"
                set file_index "?" 
                set wallpaper (jq --arg url "https://www.gstatic.com/prettyearth/assets/full/$value.jpg" '.[] | select(.image == $url)' $earthviews_json)
            case i index
                echo "in -i"
                set file_index $value
                set wallpaper (jq .[$file_index] $earthviews_json)
            case r random
                set file_index (_gen_random)
                set wallpaper (jq .[$file_index] $earthviews_json)
            case s search
                jq --arg c $value '.[] | select(.country == $c)' $earthviews_json
                return
        end
    end

    if test "$wallpaper" = ""
        echo wallpaper not set
        return
    end

    set -l wallpaper_url (echo $wallpaper | jq --raw-output .image)

    echo Country: (echo $wallpaper | jq --raw-output .country), 
    echo Region: (echo $wallpaper | jq --raw-output .region)
    echo Map: (echo $wallpaper | jq --raw-output .map)
    echo Url: $wallpaper_url, 
    echo File Index: $file_index, 

    set -l storage_path $wallpapers_dir/(basename $wallpaper_url)

    if not test -e $storage_path
        _download_wallpaper $wallpaper_url $storage_path
    else
        echo Cached wallpaper
    end

    _set_wallpaper $storage_path
end

main $argv

# Par région ou country : jq --raw-output --arg region "Ngari" '.[] | select(.region == $region) | .image' earthview.json
# si country et plusieurs régions, offrir un choix.
# Télécharger le json de github
# déplacer la cache et le json dans un autre dossier standard : ~/.cache OU vs XDG ?
